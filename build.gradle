plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id "me.modmuss50.mod-publish-plugin" version "0.8.3"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {url = 'https://jitpack.io'}
	maven {url "https://maven.terraformersmc.com/releases/"}
	maven {url = "https://api.modrinth.com/maven"}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"pucks-parity-mod" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and create separate worlds
	}

}

fabricApi {
	if (stonecutter.eval(stonecutter.current.project, "<1.21.4")) {
		configureDataGeneration()
	} else {
		configureDataGeneration {
			client = true
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${stonecutter.current.project}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
	modImplementation "com.github.Chocohead:Fabric-ASM:v${project.fabricasm_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version,
				"minecraft_version" : project.minecraft_version
	}

	inputs.property "minecraft", stonecutter.current.version

	filesMatching("fabric.mod.json") {
		expand "minecraft": stonecutter.current.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	def java = stonecutter.eval(stonecutter.current.project, ">=1.20.5")
			? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
	targetCompatibility = java
	sourceCompatibility = java
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishMods {
	changelog = "- Removed metal parity tools. These will be relegated to their own separate mod.\n- Added redstone and soul pumpkins\n- Added support for multiple versions: currently 1.19.2, 1.20.1, and 1.21.4"
	type = STABLE
	file = remapJar.archiveFile
	modLoaders.add("fabric")
	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "dzWL2rgN"
		minecraftVersions.add("1.19.2")
		minecraftVersions.add("1.20.1")
		minecraftVersions.add("1.21.4")
	}
}